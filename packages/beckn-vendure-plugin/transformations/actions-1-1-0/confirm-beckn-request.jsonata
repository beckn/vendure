(
    $message := becknRequest.body.message;
    $order := $message.order;
    $items := $message.order.items;
    $fulfillments := $message.order.fulfillments;
    $billing := $order.billing;
    $stop := $order.fulfillments[0].stops[0];
    $shippingAddress := $stop.location;
    $payment := $order.payments[0];

    $itemToOrderLine := function($tItem){(
        $fulfillment := $fulfillments[$.id = $tItem.fulfillment_ids[0]];
        $startTime := $fulfillment.stops[0].time.range.start;
        $endTime := $fulfillment.stops[0].time.range.end;
        $startEndTimePresent := $exists($startTime) and $exists($endTime);
        $retVal := {
            "productVariantId": $tItem.id,
            "quantity": $tItem.quantity.selected.count
        };
        $startEndTimePresent ? $merge([$retVal,{"startTime": $startTime, "endTime": $endTime }]) : $retVal
    )};    

    $firstName := function($fullName){
        $split($fullName," ")[0]
    };
    $lastName := function($fullName){(
        $parts := $split($fullName," ");
        $remainingParts := $filter($parts, function ($v, $i) {
            $i != 0
            });
        $lastName := $join($remainingParts," ");
        $lastName ? $lastName : "N/A";
    )};
    {
        "totalItems":$count($items),
        "firstItem": $itemToOrderLine($items[0]),
        "restItems": [$filter($items, function ($v, $i) {
            $i != 0
            }).$itemToOrderLine($)],
        "shippingMethodType": $order.fulfillments[].type,
        "setOrderShippingAddressInput":{
            "shippingAddress": $shippingAddress.{
                "fullName": $stop.contact.name,
                "streetLine1":address,
                "city":city.name,
                "province":state.name,
                "countryCode":$substring(country.code,0,2),
                "postalCode":area_code,
                "phoneNumber":$stop.contact.phone
            }
        },
        "setOrderBillingAddressInput":{
            "billingAddress": $billing.{
                "fullName": name,
                "streetLine1": address,
                "city": city.name,
                "province": state.name,
                "postalCode": area_code,
                "countryCode": $shippingAddress.country.code ? $substring($shippingAddress.country.code,0,2) : 'IN',
                "phoneNumber": phone
            }
        },
        "setCustomerForOrderInput": {
            "customerDetails": $billing.{
                "firstName": $firstName(name),
                "lastName": $lastName(name),
                "phoneNumber": phone,
                "emailAddress": email
            }
        }           
    }
)