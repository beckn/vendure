(
    $context := becknRequest.body.context;
    $env := env;
    $data := graphqlResponse.body.data.search;
    $items := $data.items;
    $allCategoryIds := allCategories.values.id;
    $usedFacetIds := $distinct($items.facetValueIds);
    $allCategoryValues := allCategories.values;
    {
        "headers":{
            "Content-Type": "application/json"
        },
        "body":{
            "context":$context ~> |$|{
                "action": 'on_' & $context.action,
                "bpp_id": $env.bpp_id,
                "bpp_uri": $env.bpp_uri,
                "bpp_country": $env.bpp_country,
                "bpp_city": $env.bpp_city,
                "timestamp": $now()
                }|,
            "message": {
                "catalog": {
                    "descriptor": {
                        "name": "Vendure Shop Catalog"
                    },
                    "providers": [
                        {
                            "id": "Vendure_Default_Shop_Token",
                            "descriptor": {
                                "name": "Default Vendure Shop"
                            },
                            "categories": $allCategoryValues[id in $usedFacetIds][].{
                                "id": id,
                                "descriptor": {
                                    "code": code,
                                    "name": name
                                }
                            },
                            "items": $items[].{
                                "price":(
                                    $price := $.price.value ? $.price.value : ($.price.min+$.price.max)/2;
                                    $price := $string($formatNumber($price/100,"#0.00"));
                                    {
                                    "listed_value": $price,
                                    "currency": currencyCode,
                                    "value": $price
                                }),
                                "id" : productVariantId,
                                "category_ids": facetValueIds[][$ in $allCategoryIds],
                                "descriptor": {
                                    "name": productVariantName,
                                    "long_desc": description,
                                    "images": [
                                        {
                                            "url": productVariantAsset.preview ? productVariantAsset.preview : productAsset.preview
                                        }
                                    ]
                                },
                                "matched": true
                            }
                        }
                    ]
                }
            }
        }
    }
)