(
    $formatMoney := function($value){
        $string($formatNumber($value/100,"#0.00"))
    };
    $formatPrice := function($price,$currencyCode){
        {
            "currency":$currencyCode,
            "value":$formatMoney($price)
        }
    };
    $context := becknRequest.body.context;
    $message := becknRequest.body.message;
    $env := env;
    $order := becknOrder;
    $items := $order.lines;
    $customer := $order.customer;
    $billing := $order.billingAddress;
    $paymentDetails:= $order.customFields;
    $shippingAddress := $order.shippingAddress;
    {
        "headers":{
            "Content-Type": "application/json"
        },
        "body":{    
            "context":$context ~> |$|{
                "action": 'on_' & $context.action,
                "bpp_id": $env.bpp_id,
                "bpp_uri": $env.bpp_uri,
                "timestamp": $now()
                }|,
            "message": {
                "order": {
                    "id": getBecknOrderInput.becknOrderId,
                    "provider":{
                        "id": "Vendure_Default_Shop_Token",
                        "descriptor": {
                            "name": "Default Vendure Shop"
                        }
                    },
                    "items": $items[].{
                        "id":$.productVariant.id,
                        "descriptor":{
                            "name":$.productVariant.name,
                            "long_desc":$.productVariant.product.description,
                            "images": ($.productVariant.assets ? $.productVariant.assets : $.productVariant.product.assets)[].{
                                "url": source
                            }
                        },
                        "price":(
                            $price := $formatMoney($.unitPrice);
                            {
                            "listed_value": $price,
                            "currency": $order.currencyCode,
                            "value": $price
                        }),
                        "quantity":{
                            "selected":{
                                "count": $.quantity
                            }
                        }
                    },
                    "fulfillments": $order.shippingLines[].{
                        "type": $.shippingMethod.code,
                        "stops":[
                            {
                                "location":{
                                    "address": $shippingAddress.streetLine1 & ' ' & ($shippingAddress.streetLine2 ? $shippingAddress.streetLine2 : ''),
                                    "city":{
                                        "name": $shippingAddress.city
                                    },
                                    "state":{
                                        "name": $shippingAddress.province
                                    },
                                    "country":{
                                        "code": $shippingAddress.countryCode
                                    },
                                    "area_code": $shippingAddress.postalCode
                                },
                                "contact":{
                                    "phone": $shippingAddress.phoneNumber,
                                    "email": $customer.emailAddress
                                }
                            }
                        ],
                        "customer": {
                            "person": {
                                "name": $customer.firstName & ' ' & $customer.lastName
                            },
                            "contact": {
                                "phone": $customer.phoneNumber
                            }
                        },
                        "state": {
                            "descriptor": {
                                "code": $order.state
                            },
                            "updated_at": $now()
                        }
                    },
                    "quote":{
                        "price":$formatPrice($order.totalWithTax,$order.currencyCode),
                        "breakup":[
                            {
                                "title":"base-price",
                                "price": $formatPrice($order.subTotal,$order.currencyCode)
                            },
                            {
                                "title": "shipping",
                                "price": $formatPrice($order.shipping,$order.currencyCode)
                            },
                            {
                                "title":"taxes",
                                "price": $formatPrice($order.totalWithTax-$order.total,$order.currencyCode)
                            }
                        ]
                    },
                    "billing":{
                        "name":$billing.fullName,
                        "phone": $billing.phoneNumber,
                        "email": $customer.emailAddress,
                        "address": $billing.streetLine1,
                        "city": {
                            "name":$billing.city
                        },
                        "state":{
                            "name":$billing.province
                        }
                    },
                    "payments": [
                        {
                            "type": "PRE-FULFILLMENT",
                            "params": {
                                "amount": $paymentDetails.paymentAmount,
                                "currency": $paymentDetails.paymentCurrencyCode,
                                "transaction_id": $paymentDetails.paymentTransactionId
                            },
                            "time":{
                                "label": "Transaction timestamp",
                                "timestamp": $paymentDetails.paymentTime
                            }
                        }
                    ]
                }
            }
        }
    }
)