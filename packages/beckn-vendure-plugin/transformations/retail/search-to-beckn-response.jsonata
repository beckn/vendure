(
    $context := becknRequest.body.context;
    $env := env;
    $data := graphqlResponse.body.data.search;
    {
        "context":$context ~> |$|{
            "action": 'on_' & $context.action,
            "bpp_id": $env.bpp_id,
            "bpp_uri": $env.bpp_uri,
            "bpp_country": $env.bpp_country,
            "bpp_city": $env.bpp_city
            }|,
        "message": {
            "catalog": {
                "descriptor": {
                    "name": "Vendure Shop Catalog"
                },
                "providers": [
                    {
                        "id": "Vendure_Default_Shop_Token",
                        "descriptor": {
                            "name": "Default Vendure Shop"
                        },
                        "items":[
                            $data.items ~> |$| {
                                "id" : productVariantId,
                                "descriptor": {
                                    "name": productVariantName
                                },
                                "matched": true,
                                "price":(
                                    $price := $.price.value ? $.price.value : ($.price.min+$.price.max)/2;
                                    $price := $string($formatNumber($price/100,"#0.00"));
                                    {
                                    "listed_value": $price,
                                    "currency": currencyCode,
                                    "value": $price
                                })
                            }, ["productVariantId","productVariantName","currencyCode"] |
                        ]
                    }
                ]
            }
        }
    }
)